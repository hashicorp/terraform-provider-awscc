---
page_title: "awscc_glue_schema_version_metadata Resource - terraform-provider-awscc"
subcategory: ""
description: |-
  This resource adds Key-Value metadata to a Schema version of Glue Schema Registry.
---

# awscc_glue_schema_version_metadata (Resource)

This resource adds Key-Value metadata to a Schema version of Glue Schema Registry.

## Example Usage

### Example using the initial version id from schema

```terraform
resource "awscc_glue_schema_version_metadata" "example" {
  schema_version_id = awscc_glue_schema.example.initial_schema_version_id
  key               = "test-key"
  value             = "test-value"
}

resource "awscc_glue_schema" "example" {
  name = "example"
  registry = {
    arn = awscc_glue_registry.example.arn
  }
  data_format   = "AVRO"
  compatibility = "NONE"
  schema_definition = jsonencode(
    {
      type = "record",
      name = "r1",
      fields = [
        {
          name = "f1",
          type = "int"
        },
        {
          name = "f2",
          type = "string"
        }
      ]
    }
  )
  tags = [{
    key   = "Modified By"
    value = "AWSCC"
  }]

}
```

### Example using the version from a schema version

```terraform
resource "awscc_glue_schema_version_metadata" "example" {
  schema_version_id = awscc_glue_schema_version.example.version_id
  key               = "test-key"
  value             = "test-value"
}


resource "awscc_glue_schema_version" "example" {
  schema = {
    schema_arn = awscc_glue_schema.example.arn
  }
  schema_definition = jsonencode(
    {
      type = "record",
      name = "r1",
      fields = [
        {
          name = "f1",
          type = "int"
        },
        { name = "f3",
          type = "string"
        }
      ]
    }
  )
}

resource "awscc_glue_schema" "example" {
  name = "example"
  registry = {
    arn = awscc_glue_registry.example.arn
  }
  data_format   = "AVRO"
  compatibility = "NONE"
  schema_definition = jsonencode(
    {
      type = "record",
      name = "r1",
      fields = [
        {
          name = "f1",
          type = "int"
        },
        {
          name = "f2",
          type = "string"
        }
      ]
    }
  )
  tags = [{
    key   = "Modified By"
    value = "AWSCC"
  }]

}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `key` (String) Metadata key
- `schema_version_id` (String) Represents the version ID associated with the schema version.
- `value` (String) Metadata value

### Read-Only

- `id` (String) Uniquely identifies the resource.

## Import

Import is supported using the following syntax:

In Terraform v1.5.0 and later, the [`import` block](https://developer.hashicorp.com/terraform/language/import) can be used with the `id` attribute, for example:

```terraform
import {
  to = awscc_glue_schema_version_metadata.example
  id = "schema_version_id|key|value"
}
```

The [`terraform import` command](https://developer.hashicorp.com/terraform/cli/commands/import) can be used, for example:

```shell
$ terraform import awscc_glue_schema_version_metadata.example "schema_version_id|key|value"
```