
---
page_title: "awscc_acmpca_certificate_authority_activation Resource - terraform-provider-awscc"
subcategory: ""
description: |-
  Used to install the certificate authority certificate and update the certificate authority status.
---

# awscc_acmpca_certificate_authority_activation (Resource)

Used to install the certificate authority certificate and update the certificate authority status.

## Example Usage

### Self-Signed Root Certificate Authority Certificate

```terraform
resource "awscc_acmpca_certificate_authority_activation" "example" {
  certificate               = awscc_acmpca_certificate.example.certificate
  certificate_authority_arn = awscc_acmpca_certificate_authority.example.arn
}

resource "awscc_acmpca_certificate" "example" {
  certificate_authority_arn   = awscc_acmpca_certificate_authority.example.arn
  certificate_signing_request = awscc_acmpca_certificate_authority.example.certificate_signing_request
  signing_algorithm           = "SHA256WITHRSA"

  template_arn = "arn:${data.aws_partition.current.partition}:acm-pca:::template/SubordinateCACertificate_PathLen0/V1"

  validity = {
    type  = "YEARS"
    value = 5
  }
}

resource "awscc_acmpca_certificate_authority" "example" {
  key_algorithm     = "RSA_4096"
  signing_algorithm = "SHA512WITHRSA"
  type              = "SUBORDINATE"
  subject = {
    common_name = "example.com"
  }
}

data "aws_partition" "current" {}
```

### Certificate for Subordinate Certificate Authority

Creates a subordinate certificate authority issued by the root certificate authority using a signing request from the subordinate certificate authority.

```terraform
resource "awscc_acmpca_certificate_authority_activation" "subordinate" {
  certificate               = awscc_acmpca_certificate.subordinate.certificate
  certificate_authority_arn = awscc_acmpca_certificate_authority.subordinate.arn
  certificate_chain         = awscc_acmpca_certificate_authority_activation.root.complete_certificate_chain
}

resource "awscc_acmpca_certificate" "subordinate" {
  certificate_authority_arn   = awscc_acmpca_certificate_authority.root.arn
  certificate_signing_request = awscc_acmpca_certificate_authority.subordinate.certificate_signing_request
  signing_algorithm           = "SHA256WITHRSA"

  template_arn = "arn:${data.aws_partition.current.partition}:acm-pca:::template/SubordinateCACertificate_PathLen3/V1"

  validity = {
    type  = "YEARS"
    value = 1
  }
  depends_on = [awscc_acmpca_certificate_authority_activation.root]
}

resource "awscc_acmpca_certificate_authority" "subordinate" {
  key_algorithm     = "RSA_4096"
  signing_algorithm = "SHA512WITHRSA"
  type              = "SUBORDINATE"
  subject = {
    common_name = "sub.example.com"
  }
}

resource "awscc_acmpca_certificate_authority_activation" "root" {
  certificate               = awscc_acmpca_certificate.root.certificate
  certificate_authority_arn = awscc_acmpca_certificate_authority.root.arn
}

resource "awscc_acmpca_certificate_authority" "root" {
  key_algorithm     = "RSA_4096"
  signing_algorithm = "SHA512WITHRSA"
  type              = "ROOT"
  subject = {
    common_name = "example.com"
  }
}

resource "awscc_acmpca_certificate" "root" {
  certificate_authority_arn   = awscc_acmpca_certificate_authority.root.arn
  certificate_signing_request = awscc_acmpca_certificate_authority.root.certificate_signing_request
  signing_algorithm           = "SHA256WITHRSA"

  template_arn = "arn:${data.aws_partition.current.partition}:acm-pca:::template/RootCACertificate/V1"

  validity = {
    type  = "YEARS"
    value = 10
  }
}


data "aws_partition" "current" {}
```

### Activate Root Certificate Authority

This example demonstrates how to activate a Root Certificate Authority by importing the signed certificate and setting its status to ACTIVE.

~> This example is generated by LLM using Amazon Bedrock and validated using terraform validate, apply and destroy. While we strive for accuracy and quality, please note that the information provided may not be entirely error-free or up-to-date. We recommend independently verifying the content.

```terraform
data "aws_caller_identity" "current" {}
data "aws_region" "current" {}

data "aws_iam_policy_document" "pca_admin_policy" {
  statement {
    actions = [
      "acm-pca:IssueCertificate",
      "acm-pca:GetCertificate",
      "acm-pca:ListPermissions",
      "acm-pca:ListTags"
    ]
    resources = ["arn:aws:acm-pca:${data.aws_region.current.name}:${data.aws_caller_identity.current.account_id}:certificate-authority/*"]
    principals {
      type = "AWS"
      identifiers = [
        "arn:aws:iam::${data.aws_caller_identity.current.account_id}:root"
      ]
    }
  }
}

# Create the Private Certificate Authority
resource "awscc_acmpca_certificate_authority" "example" {
  type              = "ROOT"
  key_algorithm     = "RSA_2048"
  signing_algorithm = "SHA256WITHRSA"

  subject = {
    common_name         = "Example Corp Root CA"
    country             = "US"
    organization        = "Example Corp"
    organizational_unit = "IT"
    state_or_province   = "WA"
    locality            = "Seattle"
  }

  revocation_configuration = {
    crl_configuration = {
      enabled = false
    }
  }

  tags = [{
    key   = "Modified By"
    value = "AWSCC"
  }]
}

# Generate a CSR for the root CA
resource "awscc_acmpca_certificate" "root_ca" {
  certificate_authority_arn   = awscc_acmpca_certificate_authority.example.arn
  certificate_signing_request = awscc_acmpca_certificate_authority.example.certificate_signing_request
  signing_algorithm           = "SHA256WITHRSA"
  template_arn               = "arn:aws:acm-pca:::template/RootCACertificate/V1"
  validity = {
    type  = "YEARS"
    value = 10
  }
}

# Activate the Certificate Authority
resource "awscc_acmpca_certificate_authority_activation" "example" {
  certificate               = awscc_acmpca_certificate.root_ca.certificate
  certificate_authority_arn = awscc_acmpca_certificate_authority.example.arn
  status                   = "ACTIVE"
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `certificate` (String) Certificate Authority certificate that will be installed in the Certificate Authority.
- `certificate_authority_arn` (String) Arn of the Certificate Authority.

### Optional

- `certificate_chain` (String) Certificate chain for the Certificate Authority certificate.
- `status` (String) The status of the Certificate Authority.

### Read-Only

- `complete_certificate_chain` (String) The complete certificate chain, including the Certificate Authority certificate.
- `id` (String) Uniquely identifies the resource.

## Import

Import is supported using the following syntax:

The [`terraform import` command](https://developer.hashicorp.com/terraform/cli/commands/import) can be used, for example:

```shell
$ terraform import awscc_acmpca_certificate_authority_activation.example "certificate_authority_arn"
```
